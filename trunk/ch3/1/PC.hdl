// This file is part of the materials accompanying the book 
// "The Elements of Computing Systems" by Nisan and Schocken, 
// MIT Press. Book site: www.idc.ac.il/tecs
// File name: projects/03/1/PC.hdl

/**
 * 16-bit counter with load and reset controls.
 *
 * If reset(t-1) then out(t) = 0
 *    else if load(t-1) then out(t) = in(t-1)
 *         else if inc(t-1) then out(t) = out(t-1) + 1 (integer addition)
 *              else out(t) = out(t-1)
 */

CHIP PC {

    IN  in[16], load, inc, reset;
    OUT out[16];

    PARTS:
	Or(a=load, b=inc, out=load0);
	Or(a=load0, b=reset, out=loadreg);
	Register(load=loadreg, in=muxedin, out=out, out=regout);
	Inc16(in=regout, out=reginc);
	Mux16(b=in, a=reginc, sel=load, out=inload);
	Mux16(a=inload, b[0..15]=false, sel=reset, out=muxedin);
}
